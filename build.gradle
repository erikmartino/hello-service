plugins {
    id("com.github.johnrengelman.shadow") version "7.1.0"
    id("io.micronaut.application") version "2.0.6"
    id 'com.google.cloud.tools.jib' version '3.1.4'
}

version = "0.1"
group = "hello.service"

repositories {
    mavenCentral()
}

micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("hello.service.*")
    }
}

task editHelmChart(type: Copy) {
    from("${projectDir}/helm/hello-service") {
        exclude "**/values.yaml"
    }
    from("${projectDir}/helm/hello-service/values.yaml") {
        filter({
            it.replace('repository: hello-service', "repository: ${jib.to.image}")
        })
    }
    into "${buildDir}/helm/hello-service"
}

tasks['jibDockerBuild'].dependsOn(editHelmChart)
tasks['jib'].dependsOn(editHelmChart)

dependencies {
    implementation("io.micronaut:micronaut-management")
    annotationProcessor("io.micronaut:micronaut-http-validation")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut:micronaut-runtime")
    implementation("javax.annotation:javax.annotation-api")
    runtimeOnly("ch.qos.logback:logback-classic")
    implementation("io.micronaut:micronaut-validation")
}


application {
    mainClass.set("hello.service.Application")
}

java {
    sourceCompatibility = JavaVersion.toVersion("11")
    targetCompatibility = JavaVersion.toVersion("11")
}



